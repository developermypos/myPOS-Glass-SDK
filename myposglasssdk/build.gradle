apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.bmuschko.nexus'

def versionNumber = 5
def versionString = "1.0.4"

ext {
    bintrayRepo = 'myPOSGlassSDK'
    bintrayName = 'myposglasssdk'

    publishedGroupId = 'com.mypos'
    libraryName = 'myPOS-Glass-SDK'
    artifact = 'glasssdk'

    libraryDescription = 'This repository provides a guidance on integrating an Android app with a myPOS Glass payment solution. Once integrated, ' +
            'the app will be able to communicate and call the main myPOS Glass functionalities in order to accept card payments (Limited to VISA and Mastercard). ' +
            'myPOS-Glass-SDK is a bridge between the applications and myPOS Glass app, converting the smart device into a POS machine, allowing to initiate transaction, ' +
            'complete all steps for processing payment, make refund to the customer card account and send a custom bill slip via mail or SMS. ' +
            'The built-in functionalities of myPOS-Glass-SDK allows you to accept payments, make refunds and void the last approved transaction.'

    siteUrl = 'https://github.com/developermypos/myPOS-Glass-SDK'
    gitUrl = 'https://github.com/developermypos/myPOS-Glass-SDK.git'

    libraryVersion = versionString

    developerId = 'mypos'
    developerName = 'myPOS Developers'
    developerEmail = 'devrel@mypos.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 30
    buildToolsVersion "25.0.3"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode versionNumber
        version versionString
        versionName versionString
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        archivesBaseName = artifact // changing library artifact id
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
//                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

//Properties properties = new Properties()
//properties.load(project.rootProject.file('local.properties').newDataInputStream())

modifyPom {
    project {
        name libraryName
        description libraryDescription
        url siteUrl
        inceptionYear '2021'
        artifactId artifact

        scm {
            url siteUrl
            connection gitUrl
            developerConnection gitUrl
        }

        licenses {
            license {
                name licenseName
                url licenseUrl
                distribution 'repo'
            }
        }

        developers {
            developer {
                id developerId
                name developerName
                email developerEmail
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}